import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'io.github.goooler.shadow'
}

evaluationDependsOn(':common')

sourceSets {
    main {
        java.srcDirs = ['src']
        resources.srcDirs = ['resources']
    }
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

dependencies {
    api project(':common')
    compileOnly 'net.minestom:minestom-snapshots:a521c4e7cd'

    implementation 'io.netty:netty-all:4.1.110.Final'
}

processResources {
    inputs.property "version", project.version

    filesMatching("extension.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

shadowJar {
    archiveBaseName.set('NegativityMinestom')
    archiveAppendix.set('1.21')
    archiveClassifier.set(null)

    mergeServiceFiles()
    from(project(':common').tasks.shadowJar)
}

build.dependsOn shadowJar

parent.tasks.named('shadowJar', ShadowJar) {
    it.from(shadowJar.outputs)
}

task prepareServer(type: Copy) {
    from shadowJar
    rename { 'negativity.jar' }
    def pluginDir = System.getProperty('plugins.dir')
    if (pluginDir == null) {
        enabled(false)
    } else {
        into pluginDir
    }
}
